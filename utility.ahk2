; autohotkey v2 beta.3
;
; * utility functions for AHKv2 scripts
; * use `#Include` to import functions here into your own scripts

Invoker(WinTitle, runCommand) {
    ; WinTitle: to match the window
    ; runCommand: command used to open app
    if (hwnd := WinExist(WinTitle)) {
        minmaxState := WinGetMinMax(hwnd)
        activeState := WinActive(hwnd)

        if (minmaxState == -1) {	; minimized
            WinRestore(hwnd)
            WinActivate(hwnd)
        } else if not (activeState) {	; non-minimized & inactive
            WinActivate(hwnd)
        } else {	; non-minimized & active
            WinMinimize(hwnd)
        }
    } else {
        Run(runCommand)
        WinActivate(WinWait(WinTitle))	; activate the new window
    }
}